// SPDX-License-Identifier: AGPL-3.0-or-later

pragma solidity 0.8.17;

import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/extensions/IERC20MetadataUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "./../access-control/AccessControlMixin.sol";
import "./../library/BocRoles.sol";
import "../library/StableMath.sol";
import "../library/NativeToken.sol";
import "../price-feeds/IValueInterpreter.sol";
import "./IStrategy.sol";

/// @title BaseStrategy
/// @author Bank of Chain Protocol Inc
abstract contract BaseStrategy is IStrategy, Initializable, AccessControlMixin {
    using SafeERC20Upgradeable for IERC20Upgradeable;
    using StableMath for uint256;

    /// @inheritdoc IStrategy
    IVault public override vault;

    /// @notice The interface of valueInterpreter contract
    IValueInterpreter public valueInterpreter;

    /// @inheritdoc IStrategy
    address public override harvester;
    /// @inheritdoc IStrategy
    uint16 public override protocol;
    /// @inheritdoc IStrategy
    string public override name;

    /// @notice The list of tokens wanted by this strategy
    address[] public wants;

    /// @inheritdoc IStrategy
    bool public override isWantRatioIgnorable;

    /// @dev Modifier that checks that msg.sender is the vault or not
    modifier onlyVault() {
        require(msg.sender == address(vault));
        _;
    }

    receive() external payable {}

    fallback() external payable {}

    function _initialize(
        address _vault,
        address _harvester,
        string memory _name,
        uint16 _protocol,
        address[] memory _wants
    ) internal {
        protocol = _protocol;
        harvester = _harvester;
        name = _name;
        vault = IVault(_vault);
        valueInterpreter = IValueInterpreter(vault.valueInterpreter());

        _initAccessControl(vault.accessControlProxy());

        require(_wants.length > 0, "wants is required");
        for (uint256 i = 0; i < _wants.length; i++) {
            require(_wants[i] != address(0), "SAI");
        }
        wants = _wants;
    }

    /// @inheritdoc IStrategy
    function getVersion() external pure virtual override returns (string memory);

    /// @inheritdoc IStrategy
    function setIsWantRatioIgnorable(bool _isWantRatioIgnorable) external override isVaultManager {
        bool _oldValue = isWantRatioIgnorable;
        isWantRatioIgnorable = _isWantRatioIgnorable;
        emit SetIsWantRatioIgnorable(_oldValue, _isWantRatioIgnorable);
    }

    /// @inheritdoc IStrategy
    function getWantsInfo()
        external
        view
        virtual
        override
        returns (address[] memory _assets, uint256[] memory _ratios);

    /// @inheritdoc IStrategy
    function getWants() external view override returns (address[] memory) {
        return wants;
    }

    /// @inheritdoc IStrategy
    function getOutputsInfo() external view virtual override returns (OutputInfo[] memory _outputsInfo){
        _outputsInfo = new OutputInfo[](1);
        OutputInfo memory _info = _outputsInfo[0];
        _info.outputCode = 0;
        _info.outputTokens = wants;
    }

    /// @inheritdoc IStrategy
    function getPositionDetail()
        public
        view
        virtual
        override
        returns (
            address[] memory _tokens,
            uint256[] memory _amounts,
            bool _isUsdOrEth,
            uint256 _usdOrEthValue
        );

    /// @inheritdoc IStrategy
    function estimatedTotalAssets() external view virtual override returns (uint256) {
        (
            address[] memory _tokens,
            uint256[] memory _amounts,
            bool _isUsdOrEth,
            uint256 _usdOrEthValue
        ) = getPositionDetail();
        if (_isUsdOrEth) {
            return _usdOrEthValue;
        } else {
            uint256 _totalUsdValue;
            for (uint256 i = 0; i < _tokens.length; i++) {
                _totalUsdValue += queryTokenValue(_tokens[i], _amounts[i]);
            }
            return _totalUsdValue;
        }
    }

    /// @inheritdoc IStrategy
    function get3rdPoolAssets() external view virtual override returns (uint256);

    /// @inheritdoc IStrategy
    function reportToVault()
        external
        virtual
        override
        isKeeperOrVaultOrGovOrDelegate
    {
        vault.report();
    }

    /// @inheritdoc IStrategy
    function borrow(address[] memory _assets, uint256[] memory _amounts)
        external
        payable
        override
        onlyVault
    {
        depositTo3rdPool(_assets, _amounts);
        emit Borrow(_assets, _amounts);
    }

    /// @inheritdoc IStrategy
    function repay(
        uint256 _repayShares,
        uint256 _totalShares,
        uint256 _outputCode
    ) public virtual override onlyVault returns (address[] memory _assets, uint256[] memory _amounts) {
        require(_repayShares > 0 && _totalShares >= _repayShares, "cannot repay 0 shares");
        _assets = wants;
        uint256[] memory _balancesBefore = new uint256[](_assets.length);
        for (uint256 i = 0; i < _assets.length; i++) {
            _balancesBefore[i] = balanceOfToken(_assets[i]);
        }

        withdrawFrom3rdPool(_repayShares, _totalShares, _outputCode);
        _amounts = new uint256[](_assets.length);
        for (uint256 i = 0; i < _assets.length; i++) {
            uint256 _balanceAfter = balanceOfToken(_assets[i]);
            _amounts[i] =
                _balanceAfter -
                _balancesBefore[i] +
                (_balancesBefore[i] * _repayShares) /
                _totalShares;
        }

        transferTokensToTarget(address(vault), _assets, _amounts);

        emit Repay(_repayShares, _totalShares, _assets, _amounts);
    }

    /// @inheritdoc IStrategy
    function poolDepositQuota() public view virtual override returns (uint256) {
        return type(uint256).max;
    }

    /// @inheritdoc IStrategy
    function poolWithdrawQuota() public view virtual override returns (uint256) {
        return type(uint256).max;
    }

    /// @notice Strategy deposit funds to 3rd pool.
    /// @param _assets the address list of token to deposit
    /// @param _amounts the amount list of token to deposit
    function depositTo3rdPool(address[] memory _assets, uint256[] memory _amounts) internal virtual;

    /// @notice Strategy withdraw the funds from 3rd pool
    /// @param _withdrawShares The amount of shares to withdraw
    /// @param _totalShares The total amount of shares owned by this strategy
    /// @param _outputCode The code of output
    function withdrawFrom3rdPool(
        uint256 _withdrawShares,
        uint256 _totalShares,
        uint256 _outputCode
    ) internal virtual;

    /// @notice Return the token's balance Of this contract
    function balanceOfToken(address _tokenAddress) internal view returns (uint256) {
        if (_tokenAddress == NativeToken.NATIVE_TOKEN) {
            return address(this).balance;
        }
        return IERC20Upgradeable(_tokenAddress).balanceOf(address(this));
    }

    /// @notice Return the value of token in USD(USDi)/ETH(ETHi).
    function queryTokenValue(address _token, uint256 _amount)
        internal
        view
        returns (uint256 _valueInUsdOrEth)
    {
        if (vault.vaultType() > 0) {
            if (_token == NativeToken.NATIVE_TOKEN) {
                _valueInUsdOrEth = _amount;
            } else {
                _valueInUsdOrEth = valueInterpreter.calcCanonicalAssetValueInEth(_token, _amount);
            }
        } else {
            _valueInUsdOrEth = valueInterpreter.calcCanonicalAssetValueInUsd(_token, _amount);
        }
    }

    /// @notice Return the uint with decimal of one token
    function decimalUnitOfToken(address _token) internal view returns (uint256) {
        if (_token == NativeToken.NATIVE_TOKEN) {
            return 1e18;
        }
        return 10**IERC20MetadataUpgradeable(_token).decimals();
    }

    /// @notice Transfer `_assets` token from this contract to target address.
    /// @param _target The target address to receive token
    /// @param _assets the address list of token to transfer
    /// @param _amounts the amount list of token to transfer
    function transferTokensToTarget(
        address _target,
        address[] memory _assets,
        uint256[] memory _amounts
    ) internal {
        for (uint256 i = 0; i < _assets.length; i++) {
            uint256 _amount = _amounts[i];
            address _asset = _assets[i];
            if (_amount > 0) {
                if (_asset == NativeToken.NATIVE_TOKEN) {
                    payable(_target).transfer(_amount);
                } else {
                    IERC20Upgradeable(_asset).safeTransfer(address(_target), _amount);
                }
            }
        }
    }
}
